#include "common.h"
#include "mblur.h"

struct 	v2p
{
  float4 tc0: 		TEXCOORD0;	// Center
  float4 tc1: 		TEXCOORD1;	// LT 		 
  float4 tc2: 		TEXCOORD2;	// RB
  float4 tc3: 		TEXCOORD3;	// RT 
  float4 tc4: 		TEXCOORD4;	// LB
  float4 tc5:		TEXCOORD5;	// Left	/ Right	
  float4 tc6:		TEXCOORD6;	// Top  / Bottom 
};

//////////////////////////////////////////////////////////////////////////////////////////
uniform sampler2D 	s_distort;
uniform float4 		e_barrier;	// x=norm(.8f), y=depth(.1f), z=clr
uniform float4 		e_weights;	// x=norm, y=depth, z=clr
uniform float4 		e_kernel;	// x=norm, y=depth, z=clr

//////////////////////////////////////////////////////////////////////////////////////////
// Pixel
float4 	main		( v2p I )	: COLOR
{
  // Normal discontinuety filter
  float3 nc		=  tex2D	(s_normal,   	I.tc0);
  float4 nd;
	nd.x 		=  dot 		(nc, (float3)tex2D(s_normal,I.tc1));
	nd.y 		=  dot 		(nc, (float3)tex2D(s_normal,I.tc2));
	nd.z 		=  dot 		(nc, (float3)tex2D(s_normal,I.tc3));
	nd.w 		=  dot 		(nc, (float3)tex2D(s_normal,I.tc4));
	nd 		-= e_barrier.x	;
	nd 		=  step		(0,nd);	// bw
  float  ne 		=  saturate	(dot(nd,e_weights.x));

  // Opposite coords
  float4 tc5r 		=  I.tc5.wzyx;
  float4 tc6r 		=  I.tc6.wzyx;

  // Depth filter : compute gradiental difference: (c-sample1)+(c-sample1_opposite)
  float4 dc		=  tex2D 	(s_position, I.tc0); 
  float4 dd;
	dd.x 		=  (float)tex2D(s_position,I.tc1).z + (float)tex2D(s_position,I.tc2).z;
	dd.y 		=  (float)tex2D(s_position,I.tc3).z + (float)tex2D(s_position,I.tc4).z;
	dd.z 		=  (float)tex2D(s_position,I.tc5).z + (float)tex2D(s_position,tc5r).z;
	dd.w 		=  (float)tex2D(s_position,I.tc6).z + (float)tex2D(s_position,tc6r).z;
	dd 		=  abs(2.0f*dc.z-dd)-e_barrier.y;
	dd 		=  step		(dd,0.0f);		// bw
  float  de 		=  saturate	(dot(dd,e_weights.y));

  // weight
  float 	w 		=  (1.0f-de*ne)*e_kernel.x;	// 0 - no aa, 1=full aa

#ifdef 	USE_DISTORT
	float4 	distort	= tex2D		(s_distort, I.tc0)	;
	float2	doffs	= (distort.xy-0.5f)*def_distort		;
	float2	center	= I.tc0 + doffs;
#else
	float2	center 	= I.tc0;
#endif

  // Smoothed color
  // (a-c)*w + c = a*w + c(1-w)

	float2	offset 	=  center * (1-w);
  float4 	s0 	=  tex2D	(s_image, offset + I.tc1*w);
  float4 	s1 	=  tex2D	(s_image, offset + I.tc2*w);
  float4 	s2 	=  tex2D	(s_image, offset + I.tc3*w);
  float4 	s3 	=  tex2D	(s_image, offset + I.tc4*w);

	float3 	final	=  mblur	(center, dc, (s0+s1+s2+s3)/4.0f);
	float3	bloom	=  tex2D	(s_bloom, I.tc0);

  return combine_bloom	(final,bloom);
}
