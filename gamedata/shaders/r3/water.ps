#include "common.h"

struct   vf
{
	float2	tbase	: TEXCOORD0;	// base
	float2	tnorm0	: TEXCOORD1;	// nm0
	float2	tnorm1	: TEXCOORD2;	// nm1
	float3	M1		: TEXCOORD3;
	float3	M2		: TEXCOORD4;
	float3	M3		: TEXCOORD5;
	float3	v2point	: TEXCOORD6;
#ifdef	USE_SOFT_WATER
#ifdef	NEED_SOFT_WATER
	float4	tctexgen: TEXCOORD7;
#endif	//	USE_SOFT_WATER
#endif	//	NEED_SOFT_WATER	
	float4	c0		: COLOR0;
	float	fog		: FOG;
	float4	hpos	: SV_Position;
};

Texture2D	s_nmap;
TextureCube	s_env0;
TextureCube	s_env1;

Texture2D	s_leaves;

#if defined(USE_SOFT_WATER) && defined(NEED_SOFT_WATER)
float3	water_intensity;
#endif	//	defined(USE_SOFT_WATER) && defined(NEED_SOFT_WATER)

////////////////////////////////////////////////////////////////////////////////
// Pixel

float4 main( vf I ) : SV_Target
{
//	float4	base	= tex2D (s_base,I.tbase);
//	float3	n0	= tex2D (s_nmap,I.tnorm0);
//	float3	n1	= tex2D (s_nmap,I.tnorm1);
	float4	base= s_base.Sample( smp_base, I.tbase);
	float3	n0	= s_nmap.Sample( smp_base, I.tnorm0);
	float3	n1	= s_nmap.Sample( smp_base, I.tnorm1);
	float3	Navg	= n0 + n1 - 1;

	float3	Nw	= mul (float3x3(I.M1, I.M2, I.M3), Navg);
			Nw	= normalize (Nw);
	float3	v2point	= normalize (I.v2point);
	float3	vreflect= reflect(v2point, Nw);

	float	fresnel	= saturate (dot(vreflect,v2point));

	//	true remapping. Slow.
	float3 vreflectabs = abs(vreflect);
	float vreflectmax = max( vreflectabs.x, max(vreflectabs.y, vreflectabs.z));
	vreflect /= vreflectmax;
	vreflect.y = vreflect.y*2-1;

//	float3	env0	= texCUBE (s_env0, vreflect);
//	float3	env1	= texCUBE (s_env1, vreflect);
	float3	env0	= s_env0.Sample( smp_rtlinear, vreflect);
	float3	env1	= s_env1.Sample( smp_rtlinear, vreflect);
	float3	env	= lerp (env0,env1,L_ambient.w);
			env	*= env*2;

	float	power	= pow(fresnel,9);
	float	amount	= 0.15f+0.25f*power;	// 1=full env, 0=no env

	float3	c_reflection       = env*amount;
	float3	final              = lerp(c_reflection,base.rgb,base.a);


        // tonemap
#ifdef        USE_VTF
//                final                *= I.c0.w        ;
#else
//                 final                 *= tex2D        (s_tonemap,I.tbase).x        ;        // any TC - OK
#endif

#ifdef	NEED_SOFT_WATER

	float	alpha	= 0.75f+0.25f*power;                        // 1=full env, 0=no env

#ifdef	USE_SOFT_WATER
	//	Igor: additional depth test
	float2 PosTc = I.tctexgen.xy/I.tctexgen.z;
	gbuffer_data gbd = gbuffer_load_data( PosTc );
	float4 _P = 	float4( gbd.P, gbd.mtl );

	float3 waterPos = float3((PosTc * 2 - 1) * pos_decompression_params.xy, 1) * I.tctexgen.z;
	float waterDepth = length(waterPos - _P) * 0.75f;

	//	water fog
	float  fog_exp_intens = -4.0f;
	float fog	= 1-exp(fog_exp_intens*waterDepth);
	float3 Fc  	= float3( 0.1f, 0.1f, 0.1f) * water_intensity.r;
	final 		= lerp (Fc, final, alpha);

	alpha 		= min(alpha, saturate(waterDepth));

	alpha 		= max (fog, alpha);

	//	Leaves
	float4 leaves = s_leaves.Sample(smp_base, I.tbase);
			leaves.rgb *= water_intensity.r;
	float calc_cos = -dot(float3(I.M1.z, I.M2.z, I.M3.z),  normalize(v2point));
	float calc_depth = saturate(waterDepth*calc_cos);
	float fLeavesFactor = smoothstep(0.025f, 0.05f, calc_depth );
			fLeavesFactor *= smoothstep(0.1f, 0.075f, calc_depth );
	final		= lerp(final, leaves, leaves.a*fLeavesFactor);
	alpha		= lerp(alpha, leaves.a, leaves.a*fLeavesFactor);

#endif	//	USE_SOFT_WATER

	final	*= I.c0*2.0f;
	//	Fogging
	final 		= lerp(fog_color, final, I.fog);
	alpha		*= I.fog*I.fog;
	final *= final;

	return  float4   (final, alpha )                ;

#else	//	NEED_SOFT_WATER

	final	*= I.c0*2;
//	return  float4   (final, 1 )                ;
	//	Fogging
	final 		= lerp(fog_color, final, I.fog);
	final *= final;
	return  float4   (final, I.fog*I.fog )      ;

#endif	//	NEED_SOFT_WATER
}